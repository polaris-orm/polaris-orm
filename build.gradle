plugins {
  id "java-library"
  id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
  id 'de.undercouch.download' version '5.4.0'
  id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

description = "Polaris is Java ORM framework"


apply plugin: 'jacoco'
apply plugin: "java-test-fixtures"
apply plugin: 'cn.taketoday.application'
apply plugin: 'cn.taketoday.build.conventions'
apply plugin: 'cn.taketoday.build.optional-dependencies'

ext {
  isSnapshot = version.endsWith('SNAPSHOT')
  isReleaseVersion = !isSnapshot
  moduleProjects = []
}

configure(allprojects) {
  if (it.hasProperty("skipDocs")) {
    it.afterEvaluate {
      it.getTasks().matching(task -> {
        return JavaBasePlugin.DOCUMENTATION_GROUP == task.getGroup()
                || "distribution" == task.getGroup()
      }).forEach(task -> task.setEnabled(false))
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()

    if (version.endsWith('-SNAPSHOT')) {
      maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
  }

  ext.javadocLinks = [
          "https://docs.oracle.com/en/java/javase/17/docs/api/",
          "https://jakarta.ee/specifications/platform/9/apidocs/",

  ] as String[]

  apply from: "${rootDir}/gradle/infra-module.gradle"
}

dependencies {
  optional("org.slf4j:slf4j-api")

  optional 'com.github.jsqlparser:jsqlparser:5.0'


  testImplementation 'com.google.guava:guava:33.1.0-jre'
  testImplementation 'org.zapodot:embedded-db-junit:1.0.0'
  testImplementation 'commons-dbutils:commons-dbutils:1.7'
  testImplementation 'org.hibernate:hibernate-core-jakarta:5.6.10.Final'
  testImplementation 'org.jooq:jooq:3.3.0'
  testImplementation 'org.jdbi:jdbi:2.78'
  testImplementation 'org.sql2o:sql2o:1.6.0'
  testImplementation 'org.teasoft:bee:1.9.5'
  testImplementation 'org.teasoft:honey:1.9.5'
  testImplementation 'org.mybatis:mybatis:3.5.9'
  testImplementation 'ch.qos.logback:logback-classic'
  testImplementation 'com.mysql:mysql-connector-j'

  testImplementation 'org.hsqldb:hsqldb'
  testImplementation 'com.h2database:h2'
  testImplementation 'org.apache.commons:commons-dbcp2'
  testImplementation 'com.zaxxer:HikariCP'

  testImplementation("org.junit.jupiter:junit-jupiter-api")
  testImplementation("org.junit.jupiter:junit-jupiter-params")
  testImplementation("org.junit.platform:junit-platform-suite-api")
  testImplementation("org.mockito:mockito-core")
  testImplementation("org.mockito:mockito-junit-jupiter")
  testImplementation("junit:junit")
  testImplementation("org.assertj:assertj-core")
  testImplementation 'org.projectlombok:lombok'
  testAnnotationProcessor("org.projectlombok:lombok")

  // Pull in the latest JUnit 5 Launcher API to ensure proper support in IDEs.
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
  testRuntimeOnly("org.junit.platform:junit-platform-suite-engine")
  // JSR-305 only used for non-required meta-annotations
  compileOnly("com.google.code.findbugs:jsr305")
  compileOnly("com.google.code.findbugs:findbugs")

  // Pull in the latest JUnit 5 Launcher API to ensure proper support in IDEs.
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
  testRuntimeOnly("org.junit.platform:junit-platform-suite-engine")

}

jacocoTestReport {
  reports {
    xml.required = true
    csv.required = false
    html.required = false
  }
}

infraJar {
  enabled = false
}

check {
  dependsOn jacocoTestReport
}

if (isReleaseVersion) {
  nexusPublishing {
    repositories {
      sonatype {
        username = repoUsername
        password = repoPassword
      }
    }
  }
}